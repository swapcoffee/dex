#include "gas.fc";
#include "math.fc";
#include "stdlib.fc";
#include "reservation.fc";

const int opcodes::transfer = 0xf8a7ea5;
const int opcodes::transfer_notification = 0x7362d09c;
const int opcodes::burn_notification = 0x7bdd97de;
const int opcodes::internal_transfer = 0x178d4519;
const int opcodes::excesses = 0xd53276db;
const int opcodes::provide_wallet_address = 0x2c76b973;
const int opcodes::take_wallet_address = 0xd1735400;
const int opcodes::unknown = 0xffffffff;

const int store_coins_zero_bits = 4;

{-
message$_ {X:Type} info:CommonMsgInfo
  init:(Maybe (Either StateInit ^StateInit))
  body:(Either X ^X) = Message X;

int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool
  src:MsgAddressInt dest:MsgAddressInt
  value:CurrencyCollection ihr_fee:Grams fwd_fee:Grams
  created_lt:uint64 created_at:uint32 = CommonMsgInfo;

ext_out_msg_info$11 src:MsgAddressInt dest:MsgAddressExt
  created_lt:uint64 created_at:uint32 = CommonMsgInfo;

addr_none$00 = MsgAddressExt;
addr_std$10 anycast:(Maybe Anycast) workchain_id:int8 address:bits256  = MsgAddressInt;
addr_extern$01 len:(## 9) external_address:(bits len) = MsgAddressExt;

nanograms$_ amount:(VarUInteger 16) = Grams;
currencies$_ grams:Grams other:ExtraCurrencyCollection = CurrencyCollection;

nothing$0 {X:Type} = Maybe X;
just$1 {X:Type} value:X = Maybe X;
left$0 {X:Type} {Y:Type} value:X = Either X Y;
right$1 {X:Type} {Y:Type} value:Y = Either X Y;

max coins = 1329227995784915872903807060280344575
-}

() send_cell(slice destination, int value, cell body) impure inline {
    send_raw_message(
        begin_cell()
            .store_uint(NON_BOUNCEABLE, 6)
            .store_slice(destination)
            .store_coins(value)
            .store_uint(1, 107)
            .store_ref(body)
            .end_cell(),
        SEND_MODE_REGULAR
    );
}

;; at most 519 bits
() send_builder(slice destination, builder body, int mode) impure inline {
    cell msg = begin_cell()
        .store_uint(NON_BOUNCEABLE, 6)
        .store_slice(destination)
        .store_uint(0, 107 + store_coins_zero_bits)
        .store_builder(body)
        .end_cell();
    send_raw_message(msg, mode);
}

;; at most 519 bits
() send_hash_builder(int destination_hash, int value, builder body, int mode) impure inline {
    builder msg = begin_cell()
        .store_uint(33792, 17) ;; NON_BOUNCEABLE, BASECHAIN
        .store_uint(destination_hash, 256)
        .store_coins(value)
        .store_uint(0, 107)
        .store_builder(body);
    send_raw_message(msg.end_cell(), mode);
}

() send_cell_carry_inbound(slice destination, cell body) impure inline {
    cell msg = begin_cell()
        .store_uint(NON_BOUNCEABLE, 6)
        .store_slice(destination)
        .store_uint(1, 107 + store_coins_zero_bits)
        .store_ref(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_CARRY_ALL_BALANCE);
}

() send_cell_extra_carry_inbound(slice destination, int extra_id, int extra_amount, cell body) impure inline {
    cell extra_dict = new_dict();
    slice extra_amount_slice = begin_cell().store_coins(extra_amount).end_cell().begin_parse();
    extra_dict~udict_set(32, extra_id, extra_amount_slice);

    cell msg = begin_cell()
        .store_uint(NON_BOUNCEABLE, 6)
        .store_slice(destination)
        .store_coins(0)
        .store_dict(extra_dict)
        .store_uint(1, 106)
        .store_ref(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_CARRY_ALL_BALANCE);
}

;; at most 519 bits and 3 refs
() send_builder_extra_carry_inbound(slice destination, int extra_id, int extra_amount, builder body) impure inline {
    cell extra_dict = new_dict();
    slice extra_amount_slice = begin_cell().store_coins(extra_amount).end_cell().begin_parse();
    extra_dict~udict_set(32, extra_id, extra_amount_slice);

    cell msg = begin_cell()
        .store_uint(NON_BOUNCEABLE, 6)
        .store_slice(destination)
        .store_coins(0)
        .store_dict(extra_dict)
        .store_uint(0, 106)
        .store_builder(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_CARRY_ALL_BALANCE);
}

() send_hash_cell_init_carry_inbound(int destination_hash, cell body, cell init) impure inline_ref {
    cell msg = begin_cell()
        .store_uint(33792, 17) ;; NON_BOUNCEABLE, BASECHAIN
        .store_uint(destination_hash, 256)
        .store_uint(7, 108 + store_coins_zero_bits)
        .store_ref(init)
        .store_ref(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_CARRY_ALL_BALANCE);
}

;; at most 518 bits and 3 refs
() send_hash_builder_init_carry_inbound(int destination_hash, builder body, cell init) impure inline_ref {
    cell msg = begin_cell()
        .store_uint(33792, 17) ;; NON_BOUNCEABLE, BASECHAIN
        .store_uint(destination_hash, 256)
        .store_uint(6, 108 + store_coins_zero_bits)
        .store_ref(init)
        .store_builder(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_CARRY_ALL_BALANCE);
}

;; at most 919 bits and 3 refs
() send_event_builder(builder body) impure inline {
    cell msg = begin_cell()
        ;; 6 = ext_out_msg_info$11 addr_none$00 addr_none$00
        ;; 98 = created_lt:uint64 created_at:uint32 maybe=0 either=0/1
        .store_uint(0x30 << 98, 6 + 98)
        .store_builder(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_REGULAR);
}

() send_event_cell(cell body) impure inline {
    cell msg = begin_cell()
        ;; 6 = ext_out_msg_info$11 addr_none$00 addr_none$00
        ;; 98 = created_lt:uint64 created_at:uint32 maybe=0 either=0/1
        .store_uint((0x30 << 98) + 1, 6 + 98)
        .store_ref(body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_REGULAR);
}

() send_event(builder body) impure inline {
    if (body.builder_bits() > 919) {
        send_event_cell(body.end_cell());
    } else {
        send_event_builder(body);
    }
}