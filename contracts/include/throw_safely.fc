#include "messages.fc";

const int gas::commit_and_throw = 26 + 84 + 18; ;; 18 for PUSHINT

() throw_safely_native(int excno, slice sender_address, int query_id) impure inline_ref {
    send_small_builder_carry_inbound(
        sender_address,
        - gas::commit_and_throw,
        begin_cell()
            .store_uint(opcodes::excesses, 32)
            .store_uint(query_id, 64)
    );
    commit();
    throw(excno);
}

() throw_safely_jetton(int excno, slice jetton_wallet, slice recipient, int query_id, int amount) impure inline_ref {
    send_cell_carry_inbound(
        jetton_wallet,
        - gas::commit_and_throw,
        begin_cell()
            .store_uint(opcodes::transfer, 32)
            .store_uint(query_id, 64)
            .store_coins(amount)
            .store_slice(recipient)
            .store_slice(recipient)
            .store_uint(0, 1)
            .store_coins(0)
            .store_uint(0, 1)
            .end_cell()
    );
    commit();
    throw(excno);
}

() throw_safely_extra(int excno, slice sender_address, int query_id, cell extra) impure inline_ref {
    send_small_builder_extra_carry_inbound2(
        sender_address,
        - gas::commit_and_throw,
        extra,
        begin_cell()
            .store_uint(opcodes::excesses, 32)
            .store_uint(query_id, 64)
    );
    commit();
    throw(excno);
}